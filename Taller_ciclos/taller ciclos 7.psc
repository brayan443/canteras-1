

subproceso ingresarVehiculo(ingresado Por Referencia,CANTIDADPUESTOS,puesto,propietario Por Referencia,placa Por Referencia,horaIngreso Por Referencia,ocupados Por Referencia)
	si ingresado>CANTIDADPUESTOS-1 entonces
		Escribir  "NO hay puestos disponibles";
	SiNo
		definir indice como entero;
		Escribir  "¿Donde desea realizar el ingreso?";
		Para i<-1 hasta CANTIDADPUESTOS-1 con paso 1 hacer
			si ocupados[i]=-1 Entonces
				Escribir ConvertirATexto(i+1)+" :: "+ puesto[i];
			FinSi
		FinPara
		Leer  indice;
		indice <-indice-1;
		
		Escribir  Sin Saltar "Propietario";
		Leer propietario[indice];
		
		Escribir  Sin Saltar "Placa del vehiculo";
		Leer placa[indice];
		
		Escribir  Sin Saltar "Hora de ingreso ";
		Leer horaIngreso[indice];
		
		//Almacenar el ingreso realizado
		ocupados[indice]<-indice;
		Escribir "Registro con éxito";
		Escribir  puesto[indice]+" "+placa[indice]+" "+horaIngreso[indice]+" "+propietario[indice];
		ingresado <-ingresado+1;
	FinSi
FinSubProceso

SubProceso puestosAsignados(ingresado,ocupados,CANTIDADPUESTOS,propietario,placa,puesto,horaIngreso)
	si ingresado=0 entonces
		escribir "Sin ingreso...";
	sino
		Para i<-0 Hasta CANTIDADPUESTOS-1 Con Paso 1 Hacer
			si ocupados[i]<>-1 Entonces
				escribir propietario[i]+"  "+placa[i]+"  "+puesto[i]+"  "+horaIngreso[i];
			FinSi
		FinPara
	FinSi
FinSubProceso



Proceso Parqueadero
	
	//Definición de variables
	Definir  CANTIDADPUESTOS, opcion, ingresado como entero; 
	Definir  valorMinuto Como Real;
	CANTIDADPUESTOS<-5;
	
	//Definición de vectores
	dimension placa[CANTIDADPUESTOS];
	dimension puesto[CANTIDADPUESTOS];
	dimension horaIngreso[CANTIDADPUESTOS];
	dimension horaSalida[CANTIDADPUESTOS];
	dimension propietario[CANTIDADPUESTOS];
	dimension ocupados[CANTIDADPUESTOS];
	
	//Inicializar variables
	opcion<-0;
	valorMinuto<-300;
	ingresado<-0;
	
	Para i<-1 Hasta CANTIDADPUESTOS-1 Con Paso 1 Hacer
		puesto[i]<-"A"+ConvertirATexto(i);
		ocupados[i]<--1;
	FinPara
	
	Escribir "                ###############################";
	Escribir "                # Paqueadero MaxTechnology 24/7 #";
	Escribir "                ###############################";
	Escribir "";
	
	
	Repetir
		Escribir "-------------------------------";
		Escribir "| 1. Ingresar vehiculo        |";
		Escribir "| 2. Dar salida a un vehiculo |";
		Escribir "| 3. Consultar disponibilidad |";
		Escribir "| 4. Puestos asignados        |";
		Escribir "| 5. Salir                    |";
		Escribir "-------------------------------";
		Escribir "";
		leer opcion;
		
		Segun opcion Hacer
			1:
				Borrar Pantalla;
				ingresarVehiculo(ingresado,CANTIDADPUESTOS,puesto,propietario,placa,horaIngreso,ocupados);
			2:
				Escribir "Dar salida";
			3:
				Escribir "Disponibilidad";
			4:
				Borrar Pantalla;
				puestosAsignados(ingresado,ocupados,CANTIDADPUESTOS,propietario,placa,puesto,horaIngreso);
			5:
				Borrar Pantalla;
				Escribir "Chao.";
			De Otro Modo:
				Escribir  "Opción no valida";
		FinSegun
		
	Hasta Que opcion=5
	
FinProceso


